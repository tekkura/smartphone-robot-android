apply plugin: 'de.undercouch.download'

dependencies {
    implementation 'io.github.nishkarsh:android-permissions:2.0.54'
    implementation 'org.apache.commons:commons-collections4:4.4'
}

android {
    namespace 'jp.oist.abcvlib.core'
}

project.ext.ASSET_DIR = projectDir.toString() + '/src/main/assets'

// Download default models; if you wish to use your own models then
// place them in the "assets" directory and comment out this line.
apply from:'download_models.gradle'

tasks.withType(GenerateMavenPom).all {
    doFirst {
        if (isDirty()){
            throw new GradleException("Current working directory is dirty. Use git stash or commit all your local changes before publishing.")
        }
        if (!isTagged()){
            throw new GradleException("You have commits after the most recent tag. Please add a new tag to capture the patch or updates after most recent tag")
        }
    }
}

afterEvaluate{
    publishing {
        repositories {
            maven {
                name = "GitHubPackages"
                url = "https://maven.pkg.github.com/oist/smartphone-robot-android"
                credentials {
                    username = System.getenv("GITHUB_ACTOR")
                    password = System.getenv("GITHUB_TOKEN")
                }
            }
        }
        publications {
            debug(MavenPublication) {
                from(components.debug)
                groupId = group
                artifactId = 'abcvlib'
                version = version
                pom {
                    description = "A library for controlling the OIST smartphone robots"
                    developers {
                        developer {
                            id = "topherbuckley"
                            name = "Christopher Buckley"
                            email = "topherbuckley@gmail.com"
                        }
                    }
                    scm {
                        url = "https://github.com/oist/smartphone-robot-android/commit/" + gitHash()
                        tag = scmTag()
                    }
                }
            }
        }
    }
}
